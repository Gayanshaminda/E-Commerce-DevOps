pipeline {
    agent any
    environment {
        DOCKER_COMPOSE_FILE = 'docker-compose.yml'
        TERRAFORM_DIR = 'terraform'
        ANSIBLE_DIR = 'ansible'
        AWS_REGION = 'eu-north-1'
        WORKSPACE = 'C:\\ProgramData\\Jenkins\\.jenkins\\workspace\\E-Commerce-DevOps-Pipeline'
        NO_PROXY = '*.docker.io,registry-1.docker.io'
        WSL_SSH_KEY = '/home/gayanshaminda2001/ansible/E-commerece-key-pair.pem'
        TERRAFORM_PARALLELISM = '10' // Parallel operations for Terraform
    }
    stages {
        stage('Checkout') {
            steps {
                git branch: 'main', url: 'https://github.com/Gayanshaminda/E-Commerce-DevOps.git'
            }
        }
        stage('Build Docker Images with Docker Compose') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'docker-hub-credentials',
                         usernameVariable: 'DOCKER_HUB_USERNAME', passwordVariable: 'DOCKER_HUB_PASSWORD')]) {
                        def gitCommitHash = bat(script: 'git rev-parse --short HEAD', returnStdout: true).trim().readLines().last()
                        
                        // Create a .env file for Docker Compose
                        writeFile file: '.env', text: "DOCKER_HUB_USERNAME=${DOCKER_HUB_USERNAME}\nGIT_COMMIT_HASH=${gitCommitHash}"
                        
                        // Use the .env file with docker-compose
                        bat "docker-compose -f ${DOCKER_COMPOSE_FILE} build"
                    }
                }
            }
        }
        stage('Push Docker Images') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'docker-hub-credentials',
                         usernameVariable: 'DOCKER_HUB_USERNAME', passwordVariable: 'DOCKER_HUB_PASSWORD')]) {
                        def gitCommitHash = bat(script: 'git rev-parse --short HEAD', returnStdout: true).trim().readLines().last()
                        
                        // Configure Docker to bypass proxy for Docker Hub
                        bat '''
                            echo {"proxies":{"default":{"httpProxy":"","httpsProxy":"","noProxy":"*.docker.io,registry-1.docker.io"}}} > %USERPROFILE%\\.docker\\config.json
                        '''
                        
                        // Login to Docker Hub
                        bat "echo %DOCKER_HUB_PASSWORD% | docker login -u %DOCKER_HUB_USERNAME% --password-stdin"
                        
                        // Push images with retry mechanism
                        retry(3) {
                            bat "docker push %DOCKER_HUB_USERNAME%/frontend:${gitCommitHash}"
                        }
                        
                        retry(3) {
                            bat "docker push %DOCKER_HUB_USERNAME%/backend:${gitCommitHash}"
                        }
                    }
                }
            }
        }

stage('Terraform Initialize') {
    steps {
        dir(TERRAFORM_DIR) {
            withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                 credentialsId: 'aws-credentials',
                 accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                 secretKeyVariable: 'AWS_SECRET_ACCESS_KEY']]) {
                // Use -upgrade to ensure latest providers
                bat "wsl terraform init -input=false -upgrade"
            }
        }
    }
}

        stage('Terraform Plan') {
            steps {
                dir(TERRAFORM_dir) {
                    withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                         credentialsId: 'aws-credentials',
                         accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                         secretKeyVariable: 'AWS_SECRET_ACCESS_KEY']]) {
                        script {
                            // Check if output file exists (meaning infrastructure might already exist)
                            def outputExists = bat(script: 'wsl terraform output -json &>/dev/null && echo "exists" || echo "not exists"', returnStdout: true).trim().contains("exists")
                            
                            if (outputExists) {
                                // Try to get existing IP to check if infrastructure exists
                                try {
                                    env.EXISTING_EC2_IP = bat(script: 'wsl terraform output -raw public_ip 2>/dev/null || echo ""', returnStdout: true).trim().readLines().last()
                                    echo "Found existing infrastructure with IP: ${env.EXISTING_EC2_IP}"
                                } catch (Exception e) {
                                    env.EXISTING_EC2_IP = ""
                                    echo "No existing infrastructure detected"
                                }
                            }
                            
                            // Run terraform plan with detailed exit code to automatically detect changes
                            def planExitCode = bat(script: "wsl terraform plan -detailed-exitcode -var=\"region=${AWS_REGION}\" -out=tfplan", returnStatus: true)
                            // Exit code 0 = No changes, 1 = Error, 2 = Changes present
                            env.TERRAFORM_CHANGES = planExitCode == 2 ? 'true' : 'false'
                            
                            if (env.TERRAFORM_CHANGES == 'true') {
                                echo "Infrastructure changes detected - will apply changes"
                            } else {
                                echo "No infrastructure changes detected - will skip apply stage"
                            }
                        }
                    }
                }
            }
        }
        stage('Terraform Apply') {
            when {
                expression { return env.TERRAFORM_CHANGES == 'true' }
            }
            steps {
                dir(TERRAFORM_DIR) {
                    withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                         credentialsId: 'aws-credentials',
                         accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                         secretKeyVariable: 'AWS_SECRET_ACCESS_KEY']]) {
                        // Apply with parallelism for faster resource creation
                        bat "wsl terraform apply -parallelism=${TERRAFORM_PARALLELISM} -input=false tfplan"
                        
                        // Capture the EC2 IP from Terraform output
                        script {
                            env.EC2_IP = bat(script: 'wsl terraform output -raw public_ip', returnStdout: true).trim().readLines().last()
                            // Add a small wait for EC2 instance to initialize (reduces SSH connection issues)
                            echo "Waiting 30 seconds for EC2 instance to initialize..."
                            sleep(30)
                        }
                    }
                }
            }
        }
        stage('Get Existing Infrastructure') {
            when {
                expression { return env.TERRAFORM_CHANGES == 'false' && env.EXISTING_EC2_IP?.trim() }
            }
            steps {
                dir(TERRAFORM_DIR) {
                    withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                         credentialsId: 'aws-credentials',
                         accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                         secretKeyVariable: 'AWS_SECRET_ACCESS_KEY']]) {
                        script {
                            env.EC2_IP = env.EXISTING_EC2_IP
                            echo "Using existing infrastructure with IP: ${env.EC2_IP}"
                        }
                    }
                }
            }
        }
        stage('Ansible Deployment') {
            steps {
                dir(ANSIBLE_DIR) {
                    script {
                        // Verify EC2_IP is set
                        if (!env.EC2_IP?.trim()) {
                            error "EC2 IP address not set. Cannot proceed with deployment."
                        }
                        
                        // Create directories if they don't exist
                        bat '''
                            if not exist ansible mkdir ansible
                            wsl mkdir -p /home/gayanshaminda2001/ansible
                        '''
                        
                        // Set proper permissions on existing key
                        bat '''
                            wsl chmod 600 /home/gayanshaminda2001/ansible/E-commerece-key-pair.pem
                            wsl ls -la /home/gayanshaminda2001/ansible/E-commerece-key-pair.pem
                        '''

                        withCredentials([
                            usernamePassword(credentialsId: 'docker-hub-credentials',
                                         usernameVariable: 'DOCKER_HUB_USERNAME',
                                         passwordVariable: 'DOCKER_HUB_PASSWORD')
                        ]) {
                            def gitCommitHash = bat(script: 'wsl git rev-parse --short HEAD', returnStdout: true).trim().readLines().last()
                            
                            // Create inventory file
                            writeFile file: 'temp_inventory.ini', text: """[ec2]
${env.EC2_IP} ansible_user=ubuntu ansible_ssh_private_key_file=${WSL_SSH_KEY}

[ec2:vars]
ansible_python_interpreter=/usr/bin/python3
ansible_ssh_common_args='-o StrictHostKeyChecking=no -o ConnectTimeout=60'
"""
                            // Run Ansible playbook
                            def result = bat(
                                script: "wsl ansible-playbook -i temp_inventory.ini deploy.yml -u ubuntu --private-key ${WSL_SSH_KEY} -e \"DOCKER_HUB_USERNAME=${DOCKER_HUB_USERNAME} GIT_COMMIT_HASH=${gitCommitHash}\" -vvv",
                                returnStatus: true
                            )
                            
                            if (result != 0) {
                                error "Ansible deployment failed with exit code ${result}"
                            }
                        }
                    }
                }
            }
        }
    }
    post {
        always {
            script {
                bat 'docker logout'
                try {
                    cleanWs(
                        deleteDirs: true,
                        patterns: [[pattern: '**/.git/**', type: 'EXCLUDE']]
                    )
                } catch (Exception e) {
                    echo "Warning: Workspace cleanup failed: ${e.getMessage()}"
                }
            }
        }
        success {
            echo 'Deployment completed successfully!'
            echo "Application deployed to: http://${env.EC2_IP}"
        }
        failure {
            echo 'Deployment failed!'
        }
    }
}